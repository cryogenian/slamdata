{-
Copyright 2016 SlamData, Inc.
Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at
    http://www.apache.org/licenses/LICENSE-2.0
Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
-}

module SlamData.GlobalMenu.Component
  ( component
  , Query(..)
  , module SlamData.GlobalMenu.Bus
  , AuthenticateOrPresentHelp(..)
  , State
  ) where

import SlamData.Prelude

import Control.UI.Browser as Browser
import Control.Monad.Aff.AVar as AVar
import Control.Monad.Aff.Bus as Bus
import Control.Monad.Eff as Eff
import Control.Monad.Eff.Ref as Ref
import Control.Monad.Eff.Exception as Exception

import Halogen as H
import Halogen.Component.Utils (busEventSource)
import Halogen.HTML as HH
import Halogen.HTML.Properties as HP
import Halogen.Menu.Component as Menu
import Halogen.Query.EventSource as ES
import Halogen.HTML.Events as HE

import OIDC.Crypt as Crypt

import Quasar.Advanced.Types (ProviderR)

import SlamData.AuthenticationMode as AuthenticationMode
import SlamData.Workspace.Eval.Card as EvalCard
import SlamData.GlobalError (GlobalError)
import SlamData.GlobalError as GlobalError
import SlamData.GlobalMenu.Bus (SignInMessage(..))
import SlamData.Monad (Slam)
import SlamData.Quasar as Api
import SlamData.Quasar.Auth as Auth
import SlamData.Quasar.Auth.Authentication (AuthenticationError, toNotificationOptions)
import SlamData.Quasar.Auth.Store as AuthStore
import SlamData.Wiring as Wiring
import SlamData.Workspace.Eval.Persistence as Persistence

data AuthenticateOrPresentHelp
  = Authenticate (Maybe ProviderR)
  | PresentHelp String

data Query a
  = DismissSubmenu a
  | HandleGlobalError GlobalError a
  | HandleMenuMessage (Menu.Message AuthenticateOrPresentHelp) a
  | Init a

type State =
  { loggedIn ‚à∑ Boolean
  }


type HTML = H.ParentHTML Query (Menu.Query AuthenticateOrPresentHelp) Unit Slam
type DSL = H.ParentDSL State Query (Menu.Query AuthenticateOrPresentHelp) Unit Void Slam

component ‚à∑ H.Component HH.HTML Query Unit Void Slam
component =
  H.lifecycleParentComponent
    { initialState: \_ ‚Üí { loggedIn: false }
    , render
    , eval
    , initializer: Just (H.action Init)
    , finalizer: Nothing
    , receiver: const Nothing
    }

render ‚à∑ State ‚Üí HTML
render state =
  HH.div
    [ HP.classes $ [ HH.ClassName "sd-global-menu" ] ]
    [ HH.slot unit Menu.component helpMenu $ HE.input HandleMenuMessage
    ]

eval ‚à∑ Query ~> DSL
eval (Init next) = do
  { bus } ‚Üê H.lift Wiring.expose
  H.subscribe $ busEventSource (flip HandleGlobalError ES.Listening) bus.globalError
  update
  pure next
eval (DismissSubmenu next) = do
  H.query unit $ H.action $ Menu.DismissSubmenu
  pure next
eval (HandleGlobalError error next) = case error of
  GlobalError.Unauthorized _ ‚Üí update $> next
  _ ‚Üí pure next
eval (HandleMenuMessage (Menu.Selected a) next) = do
  case a of
    Authenticate providerR ‚Üí authenticate providerR
    PresentHelp uri ‚Üí presentHelp uri
  pure next

update ‚à∑ DSL Unit
update = do
  maybeIdToken ‚Üê H.lift Auth.getIdToken
  case maybeIdToken of
    Just idToken ‚Üí do
      either
        (const retrieveProvidersAndUpdateMenu)
        putEmailToMenu
        (Eff.runPure $ Exception.try $ Crypt.readPayload idToken)
    Nothing ‚Üí
      retrieveProvidersAndUpdateMenu
  where
  putEmailToMenu ‚à∑ Crypt.Payload ‚Üí DSL Unit
  putEmailToMenu payload = do
    H.query unit
      $ H.action
      $ Menu.Set
        { chosen: Nothing
        , submenus:
            [ { label:
                fromMaybe "unknown user"
                $ map unwrap
                $ Crypt.pluckEmail
                $ payload
              , submenu:
                [ { label: "üîí Sign out"
                  , shortcutLabel: Nothing
                  , value: Authenticate Nothing
                  }
                ]
              }
            ]
            ‚äï helpMenu
        }
    H.modify _{ loggedIn = true }

  retrieveProvidersAndUpdateMenu ‚à∑ DSL Unit
  retrieveProvidersAndUpdateMenu = void do
    eProviders ‚Üê Api.retrieveAuthProviders
    H.query unit
      $ H.action
      $ Menu.Set
          { chosen: Nothing
          , submenus: case eProviders of
              Right (Just providers) ‚Üí
                let
                  makeSubmenuItem provider =
                    { label: "Sign in with " ‚äï provider.displayName
                    , shortcutLabel: Nothing
                    , value: Authenticate $ Just provider
                    }
                in
                  [ { label: "üîì Sign in"
                    , submenu: makeSubmenuItem <$> providers
                    }
                  ]
                ‚äï helpMenu
              _ ‚Üí helpMenu
          }

helpMenu ‚à∑ Array (Menu.MenuItem AuthenticateOrPresentHelp)
helpMenu =
  [ { label: "Help"
    , submenu:
      [ { label: "User guide"
        , shortcutLabel: Nothing
        , value: PresentHelp "http://docs.slamdata.com/en/v4.1/users-guide.html"
        }
      , { label: "Administrator guide"
        , shortcutLabel: Nothing
        , value: PresentHelp "http://docs.slamdata.com/en/v4.1/administration-guide.html"
        }
      , { label: "Developer guide"
        , shortcutLabel: Nothing
        , value: PresentHelp "http://docs.slamdata.com/en/v4.1/developers-guide.html"
        }
      , { label: "Helpful tips"
        , shortcutLabel: Nothing
        , value: PresentHelp "http://docs.slamdata.com/en/v4.1/helpful-tips.html"
        }
      , { label: "SQL¬≤ reference"
        , shortcutLabel: Nothing
        , value: PresentHelp "http://docs.slamdata.com/en/v4.1/sql-squared-reference.html"
        }
      , { label: "SlamDown reference"
        , shortcutLabel: Nothing
        , value: PresentHelp "http://docs.slamdata.com/en/v4.1/slamdown-reference.html"
        }
      , { label: "Troubleshooting FAQ"
        , shortcutLabel: Nothing
        , value: PresentHelp "http://docs.slamdata.com/en/v4.1/troubleshooting-faq.html"
        }
      ]
    }
  ]

authenticate ‚à∑ Maybe ProviderR ‚Üí DSL Unit
authenticate = maybe logOut logIn
  where
  keySuffix ‚à∑ String
  keySuffix =
    AuthenticationMode.toKeySuffix AuthenticationMode.ChosenProvider

  logOut ‚à∑ DSL Unit
  logOut = do
    AuthStore.removeIdToken keySuffix
    AuthStore.removeUnhashedNonce keySuffix
    AuthStore.removeProvider keySuffix
    update

  logIn ‚à∑ ProviderR ‚Üí DSL Unit
  logIn providerR = do
    { auth } ‚Üê H.lift Wiring.expose
    idToken ‚Üê H.liftAff AVar.makeVar
    H.liftAff $ Bus.write { providerR, idToken, prompt: true, keySuffix } auth.requestToken
    either signInFailure (const $ signInSuccess) =<< (H.liftAff $ AVar.takeVar idToken)

  signInSuccess ‚à∑ DSL Unit
  signInSuccess = do
    wiring ‚Üê Wiring.expose
    update
    traverse_ (lift ‚àò Persistence.queueEvalImmediate ‚àò EvalCard.toAll)
      =<< (H.liftEff $ Ref.readRef wiring.auth.retryEval)
    whenM
      (H.liftEff $ Ref.readRef wiring.auth.retrySave)
      (void $ lift $ Persistence.saveWorkspace)
    H.liftAff $ Bus.write SignInSuccess wiring.auth.signIn

  signInFailure ‚à∑ AuthenticationError ‚Üí DSL Unit
  signInFailure error = do
    { auth, bus } ‚Üê H.lift Wiring.expose
    H.liftAff do
      maybe (pure unit) (flip Bus.write bus.notify) (toNotificationOptions error)
      Bus.write SignInFailure auth.signIn

presentHelp ‚à∑ String ‚Üí DSL Unit
presentHelp = H.liftEff ‚àò Browser.newTab
