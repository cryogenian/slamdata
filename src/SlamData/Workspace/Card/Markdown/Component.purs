{-
Copyright 2016 SlamData, Inc.

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
-}

module SlamData.Workspace.Card.Markdown.Component
  ( markdownComponent
  , module SlamData.Workspace.Card.Markdown.Component.Query
  ) where

import SlamData.Prelude

import Data.StrMap as SM

import Data.Lens ((^?))

import DOM.BrowserFeatures.Detectors (detectBrowserFeatures)

import Halogen as H
import Halogen.HTML as HH
import Halogen.HTML.Events as HE
import Halogen.HTML.Properties as HP

import SlamData.Render.CSS as CSS
import SlamData.Workspace.Card.CardId as CID
import SlamData.Workspace.Card.CardType as CT
import SlamData.Workspace.Card.Component as CC
import SlamData.Workspace.Card.Markdown.Component.Query (Query(..))
import SlamData.Workspace.Card.Markdown.Component.State (State, initialState)
import SlamData.Workspace.Card.Model as Card
import SlamData.Workspace.Card.Port as Port
import SlamData.Workspace.Deck.DeckId as DID
import SlamData.Workspace.LevelOfDetails as LOD

import Text.Markdown.SlamDown.Halogen.Component as SD

type MarkdownQuery = SD.SlamDownQuery Port.VarMapValue
type HTML = CC.InnerCardParentHTML Query MarkdownQuery Unit
type DSL = CC.InnerCardParentDSL State Query MarkdownQuery Unit

markdownComponent ∷ CC.CardOptions → CC.CardComponent
markdownComponent options =
  CC.makeCardComponent CT.Markdown (H.lifecycleParentComponent
    { render: render uniqueCardId
    , eval: evalCard ⨁ evalComponent
    , initialState: const initialState
    , initializer: Just $ right $ H.action Init
    , finalizer: Nothing
    , receiver: const Nothing
    }) options
  where
  uniqueCardId =
    foldMap DID.toString options.cursor <> CID.toString options.cardId

render
  ∷ String
  → State
  → HTML
render formName st =
  case st.browserFeatures of
    Nothing → HH.div_ []
    Just browserFeatures → do
      HH.div
        [ HP.class_ CSS.form ]
        [ HH.slot
            unit
            (SD.slamDownComponent { formName, browserFeatures })
            unit
            (HE.input (map right ∘ HandleMessage))
        ]

evalCard ∷ CC.CardEvalQuery ~> DSL
evalCard = case _ of
  CC.Activate next →
    pure next
  CC.Deactivate next →
    pure next
  CC.Save k → do
    input ← fromMaybe mempty <$> H.gets _.input
    state ← fromMaybe SM.empty <$> H.query unit (H.request SD.GetFormState)
    pure ∘ k $ Card.Markdown { input, state }
  CC.Load card next → do
    case card of
      Card.Markdown { input, state } →
        void $ do
          H.modify (_ { input = Just input })
          H.query unit $ H.action (SD.SetDocument input)
          H.query unit $ H.action (SD.PopulateForm state)
      _ → pure unit
    pure next
  CC.ReceiveInput input _ next → do
    for_ (input ^? Port._SlamDown) \sd → do
      H.modify (_ { input = Just sd })
      void $ H.query unit $ H.action (SD.SetDocument sd)
    pure next
  CC.ReceiveOutput _ _ next →
    pure next
  CC.ReceiveState _ next →
    pure next
  CC.ReceiveDimensions _ reply →
    pure $ reply LOD.High

evalComponent ∷ Query ~> DSL
evalComponent = case _ of
  Init next → do
    browserFeatures ← H.liftEff detectBrowserFeatures
    H.modify (_ { browserFeatures = Just browserFeatures })
    pure next
  HandleMessage _ next → do
    H.raise CC.modelUpdate
    pure next
