{-
Copyright 2016 SlamData, Inc.

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
-}

module SlamData.Workspace.Card.Markdown.Component.State where

import SlamData.Prelude
import SlamData.Workspace.Card.Port.VarMap as VM
import SlamData.Workspace.Card.Markdown.Interpret as MDI
import Text.Markdown.SlamDown.Halogen.Component.State as SDS

import Data.BrowserFeatures (BrowserFeatures)
import Data.StrMap as SM

import Text.Markdown.SlamDown.Halogen.Component as SDH
import Text.Markdown.SlamDown.Syntax.FormField as SDF

type State =
  { browserFeatures ∷ Maybe BrowserFeatures
  , state ∷ SDS.SlamDownFormState VM.VarMapValue
  }

initialState ∷ State
initialState =
  { browserFeatures: Nothing
  , state: SM.empty
  }

formStateToVarMap
  ∷ SDH.SlamDownFormState VM.VarMapValue
  → SDH.SlamDownFormState VM.VarMapValue
  → VM.VarMap
formStateToVarMap newState oldState =
  SM.fold
    (\m k v → SM.insert k (valueForKey k v) m)
    SM.empty
    newState
  where
    valueForKey
      ∷ String
      → SDH.FormFieldValue VM.VarMapValue
      → VM.VarMapValue
    valueForKey k newField = case SM.lookup k oldState of
      Nothing → MDI.formFieldDefaultValue newField
      Just oldField → case oldField, newField of
        SDF.DropDown oldSel _, SDF.DropDown _ newOptions →
          MDI.formFieldDefaultValue (SDF.DropDown oldSel newOptions)
        SDF.CheckBoxes oldSel _, SDF.CheckBoxes _ newOptions →
          MDI.formFieldDefaultValue (SDF.CheckBoxes oldSel newOptions)
        SDF.RadioButtons oldSel _, SDF.RadioButtons _ newOptions →
          MDI.formFieldDefaultValue (SDF.RadioButtons oldSel newOptions)
        SDF.TextBox oldSel, SDF.TextBox newSel → case oldSel, newSel of
          SDF.PlainText _, SDF.PlainText _ → MDI.formFieldDefaultValue oldField
          SDF.Numeric _, SDF.Numeric _ → MDI.formFieldDefaultValue oldField
          SDF.Date _, SDF.Date _ → MDI.formFieldDefaultValue oldField
          SDF.Time _ _, SDF.Time _ _ → MDI.formFieldDefaultValue oldField
          SDF.DateTime _ _, SDF.DateTime _ _ → MDI.formFieldDefaultValue oldField
          _, _ → MDI.formFieldDefaultValue newField
        _, _ →
          MDI.formFieldDefaultValue newField
