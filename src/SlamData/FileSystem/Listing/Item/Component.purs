{-
Copyright 2016 SlamData, Inc.

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
-}

module SlamData.FileSystem.Listing.Item.Component where

import SlamData.Prelude

import Data.Lens (Lens', lens, (%~), (.~))

import Halogen as H
import Halogen.HTML.CSS as HCSS
import Halogen.HTML.Events as HE
import Halogen.HTML as HH
import Halogen.HTML.Properties as HP
import Halogen.HTML.Properties.ARIA as ARIA
import Halogen.Themes.Bootstrap3 as B

import CSS.Geometry (marginBottom)
import CSS.Size (px)

import DOM.Event.Types (MouseEvent, mouseEventToEvent)
import DOM.Event.Event (preventDefault)

import SlamData.Monad (Slam)
import SlamData.FileSystem.Listing.Item (Item(..), itemResource)
import SlamData.FileSystem.Listing.Item.Component.CSS as CSS
import SlamData.FileSystem.Resource (Resource(..), Mount(..), resourceName, resourcePath, isMount, isFile, isWorkspace, isViewMount, hiddenTopLevel)

type State =
  { item ∷ Item
  , isSearching ∷ Boolean
  , isHidden ∷ Boolean
  }

_item ∷ Lens' State Item
_item = lens _.item _{item = _}

_isSearching ∷ Lens' State Boolean
_isSearching = lens _.isSearching _{isSearching = _}

_isHidden ∷ Lens' State Boolean
_isHidden = lens _.isHidden _{isHidden = _}

data Query a
  = Select a
  | PresentActions a
  | HideActions a
  | Deselect a
  | HandleAction Message MouseEvent a
  | SetIsSearching Boolean a
  | SetIsHidden Boolean a

data Message
  = Open Resource
  | Configure Resource
  | Move Resource
  | Download Resource
  | Remove Resource
  | Share Resource
  | Selected

type HTML = H.ComponentHTML Query
type DSL = H.ComponentDSL State Query Message Slam

component ∷ State → H.Component HH.HTML Query Unit Message Slam
component initialState =
  H.component
    { initialState: const initialState
    , render
    , eval
    , receiver: const Nothing
    }

render ∷ State → HTML
render state = case state.item of
  SelectedItem _ → itemView state true true
  ActionsPresentedItem _ → itemView state false true
  Item _ → itemView state false false
  PhantomItem _ →
    HH.div
      [ HP.classes [ B.listGroupItem, CSS.phantom ] ]
      [ HH.div
          [ HP.class_ B.row ]
          [ HH.div
              [ HP.classes [ B.colXs8, CSS.itemContent ] ]
              [ HH.span_
                  [ HH.img [ HP.src "img/spin.gif" ]
                  , HH.text $ itemName state
                  ]
              ]
          ]
      ]

eval ∷ Query ~> DSL
eval (Select next) = do
  H.modify (_item %~ toggle)
  H.raise Selected
  pure next
  where
  toggle (Item r) = SelectedItem r
  toggle (ActionsPresentedItem r) = SelectedItem r
  toggle it = it
eval (Deselect next) = H.modify (_item %~ deselect) $> next
  where
  deselect (SelectedItem r) = Item r
  deselect it = it
eval (PresentActions next) = H.modify (_item %~ presentActions) $> next
  where
  presentActions (Item r) = ActionsPresentedItem r
  presentActions it = it
eval (HideActions next) = H.modify (_item %~ hideActions) $> next
  where
  hideActions (ActionsPresentedItem r) = Item r
  hideActions it = it
eval (HandleAction msg ev next) = do
  H.liftEff $ preventDefault (mouseEventToEvent ev)
  H.raise msg
  pure next
eval (SetIsSearching bool next) = H.modify (_isSearching .~ bool) $> next
eval (SetIsHidden bool next) = H.modify (_isHidden .~ bool) $> next

itemName ∷ State → String
itemName { isSearching, item } =
  let toName = if isSearching then resourcePath else resourceName
  in toName $ itemResource item

itemIsHidden ∷ Item → Boolean
itemIsHidden = hiddenTopLevel <<< itemResource

presentHiddenItem ∷ State → Boolean
presentHiddenItem = not <<< _.isHidden

presentItem ∷ State → Item → Boolean
presentItem state item = (isHidden && presentHiddenItem state) || not isHidden
  where
  isHidden = itemIsHidden item

itemView ∷ State → Boolean → Boolean → HTML
itemView state@{ item } selected presentActions | not (presentItem state item) =
  HH.text ""
itemView state@{ item } selected presentActions | otherwise =
  HH.div
    [ HP.classes itemClasses
    , HE.onClick (HE.input_ Select)
    , HE.onMouseEnter (HE.input_ PresentActions)
    , HE.onMouseLeave (HE.input_ HideActions)
    , HE.onDoubleClick $ HE.input $ HandleAction (Open (itemResource item))
    , ARIA.label label
    ]
    [ HH.div
        [ HP.class_ B.row ]
        [ HH.div
            [ HP.classes [ B.colXs8, CSS.itemContent ] ]
            [ HH.a
                [ HE.onClick $ HE.input $ HandleAction (Open (itemResource item)) ]
                [ HH.i [ iconClasses item ] []
                , HH.text $ itemName state
                ]
            ]
        , HH.div
            [ HP.classes $ [ B.colXs4, CSS.itemToolbar ] ⊕ (guard selected $> CSS.selected) ]
            [ itemActions presentActions item ]
        ]
    ]
  where
  itemClasses ∷ Array HH.ClassName
  itemClasses =
    [ B.listGroupItem ]
    ⊕ (guard selected $> B.listGroupItemInfo)
    ⊕ (if itemIsHidden item && presentHiddenItem state then [ CSS.itemHidden ] else [ ])

  label ∷ String
  label | selected  = "Deselect " <> itemName state
  label | otherwise = "Select " <> itemName state

iconClasses ∷ forall i r. Item → HP.IProp (class ∷ String | r) i
iconClasses item = HP.classes
  [ B.glyphicon
  , CSS.itemIcon
  , iconClass (itemResource item)
  ]
  where
  iconClass ∷ Resource → HH.ClassName
  iconClass (File _) = B.glyphiconFile
  iconClass (Workspace _) = B.glyphiconBook
  iconClass (Directory _) = B.glyphiconFolderOpen
  iconClass (Mount (Database _)) = B.glyphiconHdd
  iconClass (Mount (View _)) = B.glyphiconFile

itemActions ∷ Boolean → Item → HTML
itemActions presentActions item | not presentActions = HH.text ""
itemActions presentActions item | otherwise =
  HH.ul
    [ HP.classes [ B.listInline, B.pullRight ]
    , HCSS.style $ marginBottom (px zero)
    ]
    (conf ⊕ common ⊕ share)
  where
  r ∷ Resource
  r = itemResource item

  conf ∷ Array HTML
  conf = guard (isMount r) $>
    itemAction Configure "Configure" B.glyphiconWrench

  common ∷ Array HTML
  common =
    [
      itemAction Move "Move / rename" B.glyphiconMove
    , itemAction Download "Download" B.glyphiconCloudDownload
    , itemAction Remove "Remove" B.glyphiconTrash
    ]

  share ∷ Array HTML
  share = guard (isFile r || isWorkspace r || isViewMount r) $>
    itemAction Share "Share" B.glyphiconShare

  itemAction ∷ (Resource → Message) → String → HH.ClassName → HTML
  itemAction act label cls =
    HH.li_
      [ HH.button
          [ HE.onClick $ HE.input $ HandleAction $ act (itemResource item)
          , HP.title label
          , ARIA.label label
          , HP.class_ CSS.fileAction
          ]
          [ HH.i [ HP.classes [ B.glyphicon, cls ] ] [] ]
      ]
